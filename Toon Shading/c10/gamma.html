<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">
    <title> Informatica Grafica </title>
    <style>
      canvas {border: 1px solid black;}
    </style>
  </head>
  
  <body>
    <strong>Gamma: </strong><input type="range" name="Gamma" min="1" max="40" value="22">
    <strong>La: </strong><input type="color" name="La" value="#ffffff">
    <strong>Ld: </strong><input type="color" name="Ld" value="#ffffff">
    <strong>Ls: </strong><input type="color" name="Ls" value="#ffffff"><br>
    <canvas id="myCanvas" width="660" height="660">
      El navegador no soporta HTML5
    </canvas>
    <script id="myVertexShader" type="x-shader/x-vertex">
      
      uniform   mat4 projectionMatrix;
      uniform   mat4 modelViewMatrix;
      uniform   mat3 normalMatrix;
      
      attribute vec3 VertexPosition;
      attribute vec3 VertexNormal;
      
      varying vec3 N, ec;
  
      void main()  {
      
        N               = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        ec              = vec3(ecPosition);
        
        gl_Position     = projectionMatrix * ecPosition;
      
      }
      
    </script>
    
    <script id="myFragmentShader" type="x-shader/x-fragment">
      
      precision highp float;
      
      struct LightData {
        vec3 Position;
        vec3 La;
        vec3 Ld;
        vec3 Ls;
      };
      uniform LightData Light;
    
      struct MaterialData {
        vec3 Ka;
        vec3 Kd;
        vec3 Ks;
        float alpha;
      };
      uniform MaterialData Material;

      uniform float Gamma;
      varying vec3  N, ec;

      vec3 phong (vec3 N, vec3 L, vec3 V) {
      
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
          
          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }
        
        return (ambient + diffuse + specular);
      }

      void main() {

        vec3 n = normalize(N);
        vec3 L = normalize(Light.Position - ec);
        vec3 V = normalize(-ec);
      
        vec3 myColor = phong(n,L,V);

        float gammaFactor = 1.0/Gamma;
        
        myColor.r = pow(myColor.r,gammaFactor);
        myColor.g = pow(myColor.g,gammaFactor);
        myColor.b = pow(myColor.b,gammaFactor);

        gl_FragColor = vec4(myColor,1.0);
      
      }
      
    </script>
    
    <script src="common/gl-matrix-min.js"></script>
    <script src="common/materiales.js"></script>
    <script src="common/primitivas.js"></script>
    <script src="common/comun.js"></script>
    <script src="gamma.js"></script>

  </body>
  
</html>
